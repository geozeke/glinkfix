set shell := ["bash", "-eu", "-o", "pipefail", "-c"]
web_path := "htmlcov/index.html"
project_name := "glinkfix"

# Show help
default: help

# --------------------------------------------

_display_coverage:
    #!/usr/bin/env python3
    import webbrowser
    from pathlib import Path
    p = Path(".").resolve()/"{{web_path}}"
    webbrowser.open(f"file://{p}", new=2)

# Initialize the project environment
setup:
    #!/usr/bin/env bash
    if [ ! -f .init/setup ]; then
        if ! command -v uv >/dev/null 2>&1; then
            echo "{{project_name}} requires uv. See README for instructions."
            exit 1
        fi
        mkdir -p scratch .init run
        touch .init/setup
        cp ./scripts/* ./run
        find ./run -name '*.sh' -exec chmod 744 {} \;
        export UV_PYTHON_PREFERENCE=only-managed
        uv sync --frozen --no-dev
    else
        echo "Initial setup is already complete. If you are having issues, run:"
        echo
        echo "just reset"
        echo "just setup"
        echo
    fi

# --------------------------------------------

# Provision development dependencies
dev:
    #!/usr/bin/env bash
    if [ ! -f .init/setup ]; then
        echo 'Please run "just setup" first'
        exit 1
    fi
    export UV_PYTHON_PREFERENCE=only-managed
    uv sync --all-groups --frozen
    touch .init/dev

# --------------------------------------------

# Upgrade dependencies
upgrade:
    #!/usr/bin/env bash
    if [ ! -f .init/setup ]; then
        echo 'Please run "just setup" first'
        exit 1
    fi

    cp -f ./scripts/* ./run
    find ./run -name '*.sh' -exec chmod 744 {} \;

    if [ -f .init/dev ]; then
        uv sync --upgrade --all-groups
    else
        uv sync --upgrade --no-dev
    fi

# --------------------------------------------

# Sync dependencies with the lockfile (frozen)
sync:
    #!/usr/bin/env bash
    if [ ! -f .init/setup ]; then
        echo 'Please run "just setup" first'
        exit 1
    fi

    if [ -f .init/dev ]; then
        uv sync --all-groups
    else
        uv sync --no-dev
    fi

# --------------------------------------------

# Clean python runtime and build artifacts
clean:
    echo "Cleaning python runtime and build artifacts"
    rm -rf build/
    rm -rf dist/
    find . -type d -name __pycache__ -exec rm -rf {} \; -prune
    find . -type d -name .ipynb_checkpoints -exec rm -rf {} \; -prune
    find . -type d -name .pytest_cache -exec rm -rf {} \; -prune
    find . -type d -name .eggs -exec rm -rf {} \; -prune
    find . -type d -name htmlcov -exec rm -rf {} \; -prune
    find . -type d -name *.egg-info -exec rm -rf {} \; -prune
    find . -type f -name *.egg -delete
    find . -type f -name *.pyc -delete
    find . -type f -name *.pyo -delete
    find . -type f -name *.coverage -delete

# --------------------------------------------

# Reset the project state
reset: clean
    echo "Resetting project state"
    rm -rf .init .mypy_cache .ruff_cache .venv run htmlcov

# --------------------------------------------

# Generate an html code coverage report
coverage:
    coverage run -m pytest 
    coverage report -m
    coverage html
    just _display_coverage

# --------------------------------------------

# Run pytest with --tb=short option
test:
    pytest --tb=short

# --------------------------------------------

# Build package for publishing
build: 
	rm -rf dist
	uv build

# --------------------------------------------

# Publish package to pypi.org for production
publish-production: build
    #!/usr/bin/env bash
    set -a
    eval $(grep '^PYPI_' "$HOME/.secrets")
    uv publish --publish-url https://upload.pypi.org/legacy/ -t "$PYPI_PROD"

# --------------------------------------------

# Publish package to test.pypi.org for testing
publish-test: build
    #!/usr/bin/env bash
    set -a
    eval $(grep '^PYPI_' "$HOME/.secrets")
    uv publish --publish-url https://test.pypi.org/legacy/ -t "$PYPI_TEST"

# --------------------------------------------

# Generate release tags
tags:
    ./run/release_tags.sh

# --------------------------------------------

# Rebase to the main branch
rebase:
    ./run/rebaseline.sh

# --------------------------------------------

# Show available recipes
help:
    @just --list
